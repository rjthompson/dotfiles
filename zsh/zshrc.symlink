# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# shortcut to powerline repo
if [[ `uname` == 'Darwin' ]]
then
    export PWRLINE=/usr/local/lib/python2.7/site-packages
else
    export PWRLINE=$HOME/.local/lib/python2.7/site-packages/
fi

# your project folder that we can `c [tab]` to
export PROJECTS=~/Development

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
    source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
    source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit
# load specific functionality
autoload zmv

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
    source $file
done

unset config_files

export PATH="$PATH:$HOME/.rvm/bin" # Add RVM to PATH for scripting

#Add function for vi mode indicators
function zle-line-init zle-keymap-select {
    VIM_PROMPT="%{$fg_bold[yellow]%} [% N]%  %{$reset_color%}"
    INSERT_PROMPT="%{$fg_bold[yellow]%} [% I]%  %{$reset_color%}"
    RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/$INSERT_PROMPT} $EPS1"
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
export KEYTIMEOUT=1
